#version 400 core

// aktiviert den "Cave Effect"
// gerade auskommentiert, weil deaktiviert
#define EFFECT_CAVE

// kriegt einen 3D-Vektor für die Blickrichtung des Pixels als Input übergeben
// dieses Input wurde aus den Outputs des Vertex-Shaders interpoliert und ist für jeden Pixel verschieden
in vec3 pass_direction;

// gibt die Farbe des Pixels als 4D-RGBA-Vektor aus
out vec4 out_color;

// hier müssen alle selbst geschriebenen Methoden kurz beschrieben werden.
// genauer definiert werden sie dann später im Programm.
float trace(inout vec3 position, vec3 direction);
float distanceEstimation(vec3 p);
float gridDE(vec3 p);
float snakeDE(vec3 p);
float foodDE(vec3 p);

// Ortsvektor der Kamera
uniform vec3 cameraPosition;
uniform vec3[32] snakePositions;
uniform vec3 foodPosition;
uniform float foodRadius;
uniform int snakeLength;


// maximale beim RayMarching genutzte Schrittzahl
const int maxSteps = 32;
// Radius der Kugeln, aus denen die Schlange besteht
const float snakeSphereRadius = 0.05;

void main(void){
	
	// setzt die Startposition des Strahls auf die Position der Kameradd
	vec3 position = cameraPosition;
	// normalisiert den vom Vertex-Shader übergebenen Richtungsvektor, d.h. die Länge wird auf 1 gesetzt
	vec3 direction = normalize(pass_direction);
	
	// simuliert einen Lichtstrahl in die Richtung und bestimmt damit die Helligkeit des Pixels
	float brightness = trace(position,direction);
	
	// testet, welches Objekt getroffen wurde
	float d1 = 1/gridDE(position);
	float d2 = 1/snakeDE(position);
	float d3 = 1/foodDE(position);
	float sum = d1+d2+d3;
	vec4 color = d1/sum*vec4(0.5,0.5,0.5,1)+d2/sum*vec4(0.5,1,0,1)+d3/sum*vec4(1,0.75,0,1);
	
	color.rgb *= brightness;
	out_color = color;
	
}

// simuliert einen Lichtstrahl von einen gegebenen Punkt aus in die gegebene Richtung
float trace(inout vec3 position, vec3 direction){
	float totalDistance = 0;
	int steps;
	vec3 p;
	float distance;
	float prevDistance;
	for (steps=0;steps<maxSteps;steps++){
		// berechnet die derzeitige Position des Lichtstrahls
		p = position+totalDistance*direction;
		// berechnet die Distanz zu den zu rendernden Objekten
		prevDistance = distance;
		distance = distanceEstimation(p);
		// bewegt den Strahl um die errechnete Distanz vorwärts
		// dadurch bewegt er sich nach vorne, kann sich aber nicht durch die Objekte durchbewegen
		totalDistance += distance;
		if (distance<0.001){
			// beendet den Loop, wenn der Strahl den Objekten ausreichend nahegekommen ist
			break;
		}
	}
	// ändert die Startposition zur derzeitigen Position
	position += totalDistance*direction;
	if (steps==maxSteps){
		// gibt 0 als Helligkeit zurück, falls der Strahl die Objekte nicht erreicht hat
		return 0;
	}else{
		// errechnet die Helligkeit aus der Anzahl der benötigten Schritte bis zum Erreichen des Objekts sowie der letzten Distanz zum Objekt
		return 1.0-(float(steps)-(0.001-distance)/(prevDistance-distance))/float(maxSteps);
	}
}

// berechnet die Distanz des gegebenen Punktes zu den zu rendernden Objekten
float distanceEstimation(vec3 p){
	// prüft, ob der "Cave Effect" eingesetzt werden soll
	#ifdef EFFECT_CAVE
		// witziger Code zur Bildverzerrung
		float d = length(p-cameraPosition);
		return min(gridDE(p),min(snakeDE(p),foodDE(p)))-0.05*d*d*d*d;
	#else
		// normaler Code
		return min(gridDE(p),min(snakeDE(p),foodDE(p)));
	#endif
}

// berechnet die Distanz des gegebenen Punktes zu den Koordinatenachsen
float gridDE(vec3 p){
	// projiziert alle Punkte in den Raum von -0.5 bis +0.5, sodass der sich immer wieder wiederholt
	p = mod(p,1)-vec3(0.5);
	// berechnet die Distanz zu den Koordinatenachsen
	p = max(abs(p)-vec3(0.02),vec3(0));
	p = vec3(length(p.xy),length(p.yz),length(p.zx));
	return min(p.x,min(p.y,p.z));
}

// berechnet die Distanz des gegebenen Punktes zur Schlange
float snakeDE(vec3 p){
	float d = 100;
	for (int i=0;i<snakeLength;i++){
		d = min(d,length(mod(p-snakePositions[i]+vec3(0.5),1)-vec3(0.5)));
	}
	return max(d-snakeSphereRadius,2*snakeSphereRadius-length(p-cameraPosition));
}

// berrechnet die Distanz des gegebenen Punktes zum Futter
float foodDE(vec3 p){
	return length(mod(p-foodPosition+vec3(0.5),1)-vec3(0.5))-foodRadius;
}

